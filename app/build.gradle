plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.ksp)
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
}

// Git 태그에서 버전 정보를 가져오는 함수
def getVersionFromGit() {
    try {
        def currentTag = 'git describe --exact-match --tags HEAD'.execute().text.trim()
        if (currentTag) {
            return currentTag.startsWith('v') ? currentTag.substring(1) : currentTag
        }
    } catch (Exception ignored) {
        try {
            def tagInfo = 'git describe --tags --abbrev=0'.execute().text.trim()
            def commitCount = 'git rev-list --count HEAD'.execute().text.trim()
            def shortHash = 'git rev-parse --short HEAD'.execute().text.trim()
            if (tagInfo) {
                def baseVersion = tagInfo.startsWith('v') ? tagInfo.substring(1) : tagInfo
                return "${baseVersion}-dev.${commitCount}.${shortHash}"
            }
        } catch (Exception e2) {
            // ignore
        }
    }
    return "1.0"
}

// Git 커밋 수로 버전 코드 생성 (단조증가 보장 필요)
def getVersionCodeFromGit() {
    try {
        def commitCount = 'git rev-list --count HEAD'.execute().text.trim()
        return commitCount.toInteger()
    } catch (Exception ignored) {
        return 1
    }
}

android {
    namespace 'com.inik.camcon'
    compileSdk 35

    // 키스토어 설정 로드
    def keystorePropertiesFile = rootProject.file("key.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        // debug 키는 Android Gradle Plugin 기본 디버그 키 사용
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    defaultConfig {
        applicationId "com.inik.camcon"
        minSdk 29
        targetSdk 35
        versionCode getVersionCodeFromGit()
        versionName getVersionFromGit()
        ndk { abiFilters "arm64-v8a" }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 지원하는 언어 설정
        resConfigs "en", "ko", "it", "fr", "de", "ja"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
//            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "boolean", "SHOW_DEVELOPER_FEATURES", "true"
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "SHOW_DEVELOPER_FEATURES", "false"
        }

//        // 릴리즈 최적화 + 디버깅 가능
//        releaseDebug {
//            initWith release
//            debuggable true
//            minifyEnabled false
//            shrinkResources false
//            applicationIdSuffix ".release.debug"
//            versionNameSuffix "-release-debug"
//            matchingFallbacks = ['release', 'debug']
//            buildConfigField "boolean", "SHOW_DEVELOPER_FEATURES", "true"
//        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    // CMake 빌드 타입 스위치
    android.buildTypes.all { buildType ->
        externalNativeBuild.cmake {
            if (buildType.name == 'debug') {
                arguments '-DCMAKE_BUILD_TYPE=Debug'
            } else if (buildType.name == 'releaseDebug') {
                arguments '-DCMAKE_BUILD_TYPE=RelWithDebInfo'
            } else {
                arguments '-DCMAKE_BUILD_TYPE=Release'
            }
            cFlags '-g'
            cppFlags '-g'
        }
    }

    sourceSets {
        main { jniLibs.srcDirs = ['src/main/jniLibs'] }
    }

    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += [
                "-opt-in=androidx.compose.material.ExperimentalMaterialApi",
                "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
                "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        ]
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
            keepDebugSymbols += ['**/*.so']
        }
    }

}

// KSP configuration for better performance
ksp {
    arg("dagger.hilt.shareTestComponents", "true")
    arg("dagger.fastInit", "enabled")
    arg("dagger.formatGeneratedSource", "disabled")
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.activity

    // === Compose BOM으로 버전 통일 ===
    implementation platform('androidx.compose:compose-bom:2024.12.01')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.runtime:runtime-livedata"
    debugImplementation "androidx.compose.ui:ui-tooling"

    // Material 3 - BOM 호환 버전 사용
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material3:material3-window-size-class"
    // 버전 충돌 문제로 임시 제거
    // implementation "androidx.compose.material3:material3-adaptive:1.0.0-alpha06"

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    //hilt
    implementation libs.hilt.android
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'
    ksp libs.hilt.compiler

    // 코루틴 - 최신 버전으로 업데이트
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.2'
    // Firebase await() 사용

    implementation "com.google.android.material:material:1.12.0"

    implementation "androidx.activity:activity-compose:1.10.1"
    implementation("androidx.activity:activity-ktx:1.10.1")

    // Navigation Compose
    implementation "androidx.navigation:navigation-compose:2.7.7"

    // 소셜 로그인 - 구글만 사용
    implementation 'com.google.android.gms:play-services-auth:21.1.1' // 구글 로그인
    implementation platform('com.google.firebase:firebase-bom:33.4.0')
    implementation 'com.google.firebase:firebase-auth-ktx'

    // DataStore for preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    //
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.32.0"

    // Image loading - 최신 버전
    implementation "io.coil-kt:coil-compose:2.7.0"

    // Foundation - BOM으로 버전 관리
    implementation "androidx.compose.foundation:foundation"

    implementation("com.google.firebase:firebase-auth")
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation("androidx.credentials:credentials:1.3.0")
    implementation("androidx.credentials:credentials-play-services-auth:1.3.0")
    implementation("com.google.android.libraries.identity.googleid:googleid:1.1.1")
    implementation 'com.google.firebase:firebase-messaging-ktx'
    // ExifInterface for preserving image metadata
    implementation "androidx.exifinterface:exifinterface:1.4.1"
    // GPUImage for color transfer and image processing
    implementation 'jp.co.cyberagent.android:gpuimage:2.1.0'
    // 0xZhangKe ImageViewer library for advanced image viewing
    implementation 'com.github.0xZhangKe:ImageViewer:1.0.3'
    // 기본 Compose 제스처를 사용한 이미지 뷰어 구현

    // ZoomImage with Coil integration for Compose - 최신 버전 사용
    implementation "io.github.panpf.zoomimage:zoomimage-compose-coil2:1.4.0-beta04"

    // GPUImage for image processing
    implementation 'jp.co.cyberagent.android:gpuimage:2.1.0'

    // ExifInterface for metadata handling
    implementation 'androidx.exifinterface:exifinterface:1.3.7'

    // Gson for JSON serialization
    implementation 'com.google.code.gson:gson:2.10.1'

    // Google Play Billing for subscription management
    implementation 'com.android.billingclient:billing-ktx:6.1.0'

    // Toasty for showing stylish notifications
    implementation 'com.github.GrenderG:Toasty:1.5.2'

    // Firebase Remote Config for app update management
    implementation 'com.google.firebase:firebase-config-ktx'

    // HTTP client for version checking (backup method)
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
}

// 커스텀 빌드 태스크들
task buildDebug(type: GradleBuild) {
    description = '디버그 모드로 빌드 및 설치'
    tasks = ['assembleDebug', 'installDebug']
}

task buildRelease(type: GradleBuild) {
    description = '릴리즈 모드로 빌드'
    tasks = ['assembleRelease']
}

task buildReleaseDebug(type: GradleBuild) {
    description = '릴리즈 성능 + 디버깅 가능 모드로 빌드 및 설치'
    tasks = ['assembleReleaseDebug', 'installReleaseDebug']
}

task runDebug(type: Exec) {
    description = '디버그 앱 실행'
    dependsOn 'installDebug'
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.inik.camcon.debug/.presentation.ui.MainActivity'
}

task runRelease(type: Exec) {
    description = '릴리즈 앱 실행'
    dependsOn 'installRelease'
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.inik.camcon/.presentation.ui.MainActivity'
}

task runReleaseDebug(type: Exec) {
    description = '릴리즈 디버그 앱 실행'
    dependsOn 'installReleaseDebug'
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.inik.camcon/.presentation.ui.MainActivity'
}
